buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "net.serenity-bdd:serenity-single-page-report:4.2.34"
    }
}

plugins {
    id 'java'
    id "net.serenity-bdd.serenity-gradle-plugin" version "4.2.34"
}

group = 'com.co.automation.newproject'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    serenityVersion = '4.2.34'
    cucumberVersion = '7.18.0'
}

dependencies {
    // ---- USO EN main ----
    implementation "net.serenity-bdd:serenity-core:${serenityVersion}"
    implementation "net.serenity-bdd:serenity-model:${serenityVersion}"
    implementation "net.serenity-bdd:serenity-screenplay:${serenityVersion}"
    implementation "net.serenity-bdd:serenity-screenplay-webdriver:${serenityVersion}"
    testImplementation "net.serenity-bdd:serenity-ensure:${serenityVersion}"
    implementation "org.seleniumhq.selenium:selenium-java:4.25.0"

    // ---- SOLO PARA TEST ----
    testImplementation "net.serenity-bdd:serenity-junit:${serenityVersion}"
    testImplementation "net.serenity-bdd:serenity-cucumber:${serenityVersion}"
    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.assertj:assertj-core:3.26.3"

    implementation "org.slf4j:slf4j-reload4j:2.0.7"
    implementation "org.apache.logging.log4j:log4j-core:2.20.0"
    implementation "org.antlr:ST4:4.3.4"
}

tasks.register('backupFeatures') {
    dependsOn(clean)
    doLast {
        copy {
            from "src/test/resources/features"
            into "build/originalFeatures"
        }
    }
}

tasks.register('restoreFeatures') {
    doLast {
        copy {
            from "build/originalFeatures"
            into "src/test/resources/features"
        }
        delete "build/originalFeatures"

        copy {
            from "src/test/resources/assets"
            into "target/site/serenity/assets"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

compileJava.dependsOn(backupFeatures)

serenity {
    requirementsDir = "src/test/resources/features"
    reports = ["single-page-html"]
}

gradle.startParameter.continueOnFailure = true

test {
    useJUnit()
    include '**/WebRunnerTest.class'
    finalizedBy(aggregate,  restoreFeatures)
}